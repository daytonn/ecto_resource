defmodule EctoResource do
  @moduledoc """
  This module represents the generic CRUD functionality that is boilerplate within
  Phoenix context files. It provides a DSL to easily generate the basic functions
  for a schema. This allows the context to focus on interesting, atypical implementations
  rather than the redundent, drifting crud functions.

  All examples are using `Person`. This can be substituted for a Phoenix context or any other module.


      defmodule Person do
        use EctoResource

        using_repo(Repo) do
          resource(MySchema)
        end
      end

  This will generate the functions:

  `Person.change_person(changable)`
    - changable object to generate changeset for

  `Person.create_person(attributes)`
    - attributes

  `Person.delete_person(deletable)`
    - deletable: Schema object to delete

  `Person.get_person(id, options)`
    - id: id field for the schema value to be queried for
    - options:
      - preloads

  `Person.all_people(options)`
    - options:
      - preloads
      - order_by

  `Person.update_person(updatable, attributes)`
    - updatable: Schema object to update
    - attributes

  There are also introspection functions to understand what is generated by the macro
  `Person.__resource__(:resources)` - A listing of the defined resources and their generated functions

  More granular control is available through options

      defmodule Person do
        use EctoResource

        using_repo(Repo) do
          resource(MySchema, :read)
        end
      end

  This will generate the functions:

  `Person.all_persons(options)`
    - options:
      - preloads
      - order_by

  `Person.get_person(id, options)`
    - id: id field for the schema value to be queried for
    - options:
      - preloads

  `Person.__resource__(:resources)` - A listing of the defined resources and their generated functions

      defmodule Person do
        use EctoResource

        using_repo(Repo) do
          resource(MySchema, :write)
        end
      end

  This will generate the functions:

  `Person.change_person(changable)`
    - changable object to generate changeset for

  `Person.create_person(attributes)`
    - attributes

  `Person.update_person(updatable, attributes)`
    - updatable: Schema object to update
    - attributes

  `Person.__resource__(:resources)` - A listing of the defined resources and their generated functions

      defmodule Person do
        use EctoResource

        using_repo(Repo) do
          resource(MySchema, :delete)
        end
      end

  This will generate the functions:

  `Person.delete_person(deletable)`
    - deletable: Schema object to delete

  `Person.__resource__(:resources)` - A listing of the defined resources and their generated functions
  """

  import Ecto.Query

  alias __MODULE__
  alias Inflex

  @actions %{
    all: 1,
    change: 1,
    create: 1,
    delete: 1,
    get: 2,
    update: 2
  }

  @spec change(module, Ecto.Schema.t()) :: Ecto.Changeset.t()
  def change(schema, changable) do
    changable
    |> schema.changeset(%{})
  end

  @spec create(Ecto.Repo.t(), module, map()) ::
          {:ok, Ecto.Schema.t()} | {:error, Ecto.Changeset.t()}
  def create(repo, schema, attributes) do
    schema
    |> struct()
    |> schema.changeset(attributes)
    |> repo.insert([])
  end

  @spec delete(Ecto.Repo.t(), Ecto.Schema.t()) ::
          {:ok, Ecto.Schema.t()} | {:error, Ecto.Changeset.t()}
  def delete(repo, deletable) do
    deletable
    |> repo.delete([])
  end

  @spec get(Ecto.Repo.t(), module, term(), term()) :: Ecto.Schema.t() | nil
  def get(repo, schema, id, options \\ []) do
    preloads = Keyword.get(options, :preloads, [])

    schema
    |> preload(^preloads)
    |> repo.get(id, [])
  end

  @spec all(Ecto.Repo.t(), module, term()) :: list(Ecto.Schema.t())
  def all(repo, schema, options \\ []) do
    preloads = Keyword.get(options, :preloads, [])
    order_by = Keyword.get(options, :order_by, [])

    schema
    |> preload(^preloads)
    |> order_by(^order_by)
    |> repo.all([])
  end

  @spec update(Ecto.Repo.t(), module, Ecto.Schema.t(), map()) ::
          {:ok, Ecto.Schema.t()} | {:error, Ecto.Changeset.t()}
  def update(repo, schema, updateable, attributes) do
    updateable
    |> schema.changeset(attributes)
    |> repo.update([])
  end

  def underscore_module_name(module) do
    module
    |> Macro.underscore()
    |> String.split("/")
    |> List.last()
  end

  defmacro __using__(_) do
    quote do
      import EctoResource, only: [using_repo: 2]
    end
  end

  defmacro using_repo(repo, do: block) do
    quote do
      Module.register_attribute(__MODULE__, :repo, [])
      Module.put_attribute(__MODULE__, :repo, unquote(repo))
      Module.register_attribute(__MODULE__, :resources, accumulate: true)
      import EctoResource, only: [resource: 2, resource: 1]
      unquote(block)
      def __resource__(:resources), do: @resources
      Module.delete_attribute(__MODULE__, :resources)
      Module.delete_attribute(__MODULE__, :repo)
    end
  end

  defp action_arity(verb), do: @actions[verb]

  defp accumulate_action_metadata(suffix, action, acc) do
    arity = action_arity(action)

    name =
      case action do
        :all -> :"#{action}_#{Inflex.pluralize(suffix)}"
        _ -> :"#{action}_#{suffix}"
      end

    Map.put(acc, action, %{
      description: "#{name}/#{arity}",
      name: name
    })
  end

  def resource_actions(suffix, only: subset) do
    @actions
    |> Map.keys()
    |> Enum.filter(fn action ->
      Enum.member?(subset, action)
    end)
    |> Enum.reduce(%{}, &accumulate_action_metadata(suffix, &1, &2))
  end

  def resource_actions(suffix, except: excluded_set) do
    @actions
    |> Map.keys()
    |> Enum.reject(fn action ->
      Enum.member?(excluded_set, action)
    end)
    |> Enum.reduce(%{}, &accumulate_action_metadata(suffix, &1, &2))
  end

  def resource_actions(suffix, :read) do
    @actions
    |> Map.keys()
    |> Enum.filter(fn action ->
      Enum.member?([:all, :get], action)
    end)
    |> Enum.reduce(%{}, &accumulate_action_metadata(suffix, &1, &2))
  end

  def resource_actions(suffix, :write) do
    @actions
    |> Map.keys()
    |> Enum.filter(fn action ->
      Enum.member?([:change, :create, :update], action)
    end)
    |> Enum.reduce(%{}, &accumulate_action_metadata(suffix, &1, &2))
  end

  def resource_actions(suffix, :delete) do
    @actions
    |> Map.keys()
    |> Enum.filter(fn action ->
      Enum.member?([:delete], action)
    end)
    |> Enum.reduce(%{}, &accumulate_action_metadata(suffix, &1, &2))
  end

  defmacro resource(schema, options \\ [only: [:all, :change, :create, :delete, :get, :update]]) do
    quote bind_quoted: [schema: schema, options: options] do
      resources =
        EctoResource.resource_actions(EctoResource.underscore_module_name(schema), options)

      descriptions =
        resources
        |> Map.values()
        |> Enum.map(& &1.description)

      Module.put_attribute(
        __MODULE__,
        :resources,
        {@repo, schema, descriptions}
      )

      resources
      |> Map.keys()
      |> Enum.each(fn action ->
        resource_action = Map.put(%{}, action, resources[action])

        case resource_action do
          %{all: %{name: name}} ->
            def unquote(name)(options \\ []),
              do: EctoResource.all(@repo, unquote(schema), options)

          %{change: %{name: name}} ->
            def unquote(name)(changable),
              do: EctoResource.change(unquote(schema), changable)

          %{create: %{name: name}} ->
            def unquote(name)(attributes),
              do: EctoResource.create(@repo, unquote(schema), attributes)

          %{delete: %{name: name}} ->
            def unquote(name)(deletable),
              do: EctoResource.delete(@repo, deletable)

          %{get: %{name: name}} ->
            def unquote(name)(id, options \\ []),
              do: EctoResource.get(@repo, unquote(schema), id, options)

          %{update: %{name: name}} ->
            def unquote(name)(updatable, attributes),
              do: EctoResource.update(@repo, unquote(schema), updatable, attributes)

          _ ->
            nil
        end
      end)
    end
  end
end
